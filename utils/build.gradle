import java.util.regex.Pattern

apply plugin: 'java-library'
apply plugin: 'import-aar'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

version = parent.ext.versionName
def targetSdkVersion = parent.ext.targetSdkVersion

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testCompile group: 'junit', name: 'junit', version: '4.12'
    //region 添加android.jar依赖
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def sdkDir = properties.getProperty('sdk.dir')

    if (isGradle3()) {
        compileOnly files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar")
    } else {
        project.dependencies.add("compileOnly", files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar"))
    }
    //endregion

    api 'com.squareup.okhttp3:okhttp:3.10.0'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

private boolean isGradle3() {
    Pattern GRADLE_ACCEPTABLE_VERSIONS = Pattern.compile("4\\.\\d*|0\\.\\d*[1-9]\\d*\$")
    if (GRADLE_ACCEPTABLE_VERSIONS.matcher(project.getGradle().getGradleVersion()).matches()) {
        return true
    }
    return false
}

// 上传jcenter的工具配置
apply plugin: 'com.novoda.bintray-release'
//发布到
//发布请执行
// 加上 -x test 跳过单元测试
//gradlew clean build -x test bintrayUpload -PbintrayUser=kigkrazy -PbintrayKey=BINTRAY_KEY -PdryRun=false
publish {
    repoName = 'reizxutils'
    userOrg = 'kigkrazy'//bintray.com用户名
    groupId = 'com.reizx'//jcenter上的路径
    artifactId = 'okhttputils'//项目名称
    publishVersion = version //版本号
    desc = 'an okhttp3 wapper utils'
    website = 'https://github.com/kigkrazy/okhttputils'
}