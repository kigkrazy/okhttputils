import java.util.regex.Pattern

apply plugin: 'java-library'
apply plugin: 'import-aar'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

version = parent.ext.versionName
def targetSdkVersion = parent.ext.targetSdkVersion

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //region 添加android.jar依赖
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def sdkDir = properties.getProperty('sdk.dir')

    if (isGradle3()) {
        compileOnly files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar")
    } else {
        project.dependencies.add("compileOnly", files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar"))
    }
    //endregion

    api 'com.squareup.okhttp3:okhttp:3.10.0'
}


private boolean isGradle3() {
    Pattern GRADLE_ACCEPTABLE_VERSIONS = Pattern.compile("4\\.\\d*|0\\.\\d*[1-9]\\d*\$")
    if (GRADLE_ACCEPTABLE_VERSIONS.matcher(project.getGradle().getGradleVersion()).matches()) {
        return true
    }
    return false
}

// 上传jcenter的工具配置
apply plugin: 'com.novoda.bintray-release'

// 发布请执行：
// gradlew clean build bintrayUpload -PbintrayUser=kigkrazy -PbintrayKey=${APIKey} -PdryRun=false
publish {
    repoName = 'reizxutils'
    artifactId = 'andrutil'
    groupId = 'com.reizx'
    userOrg = 'kigkrazy'//bintray用户名
    publishVersion = version
    website = 'https://github.com/kigkrazy/andrutil'
    desc = 'an android common utils collection'//描述，不重要
}